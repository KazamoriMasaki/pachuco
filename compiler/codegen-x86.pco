;;; x86 bitness-independent code generation

;;; Assembly bits

(defconstant value-insn-size-suffix (string-ref insn-size-suffixes value-scale))

(define (emit-align cg scale)
  (emit cg ".align ~D" (ash 1 scale)))

;;; Registers and address modes
;;;
;;; - immediates are simply numbers or strings (representing assembler
;;; labels)
;;;
;;; - registers a vector of strings, giving the name of the register
;;; for the various scales
;;;
;;; - a memory reference a list of the form (offset regs...)

(define (immediate? x) (or (number? x) (string? x)))
(define (register? x) (vector? x))
(define (mem? x) (pair? x))

(define (mem-operand reg-or-imm)
  (cond ((immediate? reg-or-imm) (list reg-or-imm))
        ((register? reg-or-imm) (list 0 reg-or-imm))
        (true (error "can't make memory operand from ~A" reg-or-imm))))

(define (offset-operand reg offset)
  (unless (register? reg) (error "~A not a register" reg))
  (cond ((immediate? offset) (list offset reg))
        ((register? offset) (list 0 offset reg))
        (true (error "can't offset memory operand with ~A" offset))))

(define (offset-2-operand rega regb imm)
  (unless (register? rega) (error "~A not a register" rega))
  (unless (register? regb) (error "~A not a register" regb))
  (unless (immediate? imm) (error "~A not an immediate" imm))
  (list imm rega regb))

(define (indexed-operand reg imm)
  (unless (register? reg) (error "~A not a register" reg))
  (unless (number? imm) (error "~A not an immediate" imm))
  (list (* value-size imm) reg))

(define (tagged-operand tag reg imm)
  (unless (register? reg) (error "~A not a register" reg))
  (unless (number? imm) (error "~A not an immediate" imm))
  (list (- (* value-size imm) tag) reg))

(define (reoffset-operand base offset)
  (cond ((number? offset)
         (cons (+ offset (car base)) (cdr base)))
        ((register? offset)
         (list* (car base) offset (cdr base)))
        (true
         (error "can't offset memory operand with ~A" offset))))

(define (reoffset-2-operand base reg imm)
  (list* (+ imm (car base)) reg (cdr base)))

(define (reindex-operand base index)
  (reoffset-operand base (* value-size index)))


(defmacro (reg-asm-operand reg scale)
  (list 'register-variant reg scale))

(define (asm-operand x scale)
  (cond ((immediate? x) (format "$~A" x))
        ((vector? x) (reg-asm-operand x scale))
        ((pair? x)
         (cond ((null? (cdr x)) (absolute-asm-operand (car x)))
               ((null? (cddr x))
                (format "~A(~A)" (if (eq? 0 (car x)) "" (car x))
                         (reg-asm-operand (second x) value-scale)))
               (true
                (format "~A(~A,~A)" (if (eq? 0 (car x)) "" (car x))
                         (reg-asm-operand (third x) value-scale)
                         (reg-asm-operand (second x) value-scale)))))
        (true (error "strange operand ~S" x))))

(define (value-sized operand)
  (asm-operand operand value-scale))

(define (operand-using-reg? o r)
  (cond ((register? o) (eq? o r))
        ((mem? o) (member? r (cdr o)))
        (true false)))

;;; Operator argument constraints

(define (reg-mem-operand-constraint cg operand reg)
  (if (or (immediate? operand)
          (and (register? operand) (not (eq? operand reg))))
      (begin
        (emit-mov cg operand reg)
        reg)
      operand))

(define (reg-mem-imm-operand-constraint cg operand reg)
  (if (and (register? operand) (not (eq? operand reg)))
      (begin
        (emit-mov cg operand reg)
        reg)
      operand))

(define (reg-imm-operand-constraint cg operand reg)
  (if (or (mem? operand)
          (and (register? operand) (not (eq? operand reg))))
      (begin
        (emit-mov cg operand reg)
        reg)
      operand))

;;; Condition codes

(define (negate-cc cc)
  (if (eq? (string-ref cc 0) #\n)
      (substring cc 1 (1- (string-length cc)))
      (string-concat "n" cc)))

;;; Instructions

(defmacro (define-insn-2 name insn)
  (quasiquote
    (define ((unquote name) cg src dest . scale)
      (emit-insn-2 cg (unquote insn) src dest scale))))

(define (emit-insn-2 cg insn src dest scale)
  (set! scale (if (null? scale) value-scale (car scale)))
  (emit cg "~A~C ~A, ~A" insn (string-ref insn-size-suffixes scale)
        (asm-operand src scale) (asm-operand dest scale)))

(define-insn-2 emit-add "add")
(define-insn-2 emit-sub "sub")
(define-insn-2 emit-imul "imul")
(define-insn-2 emit-and "and")
(define-insn-2 emit-or "or")
(define-insn-2 emit-xor "xor")
(define-insn-2 emit-cmp "cmp")
(define-insn-2 emit-test "test")
(define-insn-2 emit-shl "shl")
(define-insn-2 emit-sar "sar")

(define (emit-lea cg mem reg)
  (if (and (= 0 (first mem)) (null? (cddr mem)))
      (emit-mov cg (second mem) reg)
      (emit-insn-2 cg "lea" mem reg (list value-scale))))

(define (emit-clear cg reg . scale)
  (set! scale (if (null? scale) value-scale (car scale)))
  (emit-xor cg reg reg (min 2 scale)))

(define (emit-mov cg src dest . scale)
  (cond ((and (eq? src 0) (register? dest))
         (emit-clear cg dest))
        ((and (register? src) (eq? src dest))
         ;; omit instruction
         )
        ((and (number? src) (register? dest) (> src 0) (< src 1000000))
         (emit-movzx cg src dest
                     (min 2 (if (null? scale) value-scale (car scale)))))
        (true
         (emit-insn-2 cg "mov" src dest scale))))

(define movzx-suffixes (vector (vector "b")
                               (vector "zbw" "w")
                               (vector "zbl" "zwl" "l")))

(define (emit-movzx-32 cg src dest src-scale dest-scale)
  (emit cg "mov~A ~A,~A"
        (vector-ref (vector-ref movzx-suffixes dest-scale) src-scale)
        (asm-operand src src-scale)
        (asm-operand dest dest-scale)))

(define (emit-push cg reg)
  (emit cg "push~C ~A" value-insn-size-suffix (value-sized reg)))

(define (emit-pop cg reg)
  (emit cg "pop~C ~A" value-insn-size-suffix (value-sized reg)))

(define (emit-set cg cc reg)
  (emit cg "set~A ~A" cc (asm-operand reg 0)))

(defmacro (define-insn-1 name insn)
  (quasiquote
    (define ((unquote name) cg oper . scale)
      (emit-insn-1 cg (unquote insn) oper
                   (if (null? scale) value-scale (car scale))))))

(define (emit-insn-1 cg insn oper scale)
  (emit cg "~A~C ~A" insn (string-ref insn-size-suffixes scale)
        (asm-operand oper scale)))

(define-insn-1 emit-neg "neg")
(define-insn-1 emit-not "not")
(define-insn-1 emit-idiv "idiv")

(define (emit-ret cg imm)
  (if (= 0 imm)
      (emit cg "ret")
      (emit cg "ret $~D" imm)))

(defmacro (define-insn-0 name insn)
  (quasiquote
    (define ((unquote name) cg . scale)
      (emit cg "~A~C" (unquote insn)
            (if (null? scale) value-insn-size-suffix
                (string-ref insn-size-suffixes (car scale)))))))

(define-insn-0 emit-rep-movs "rep ; movs")
(define-insn-0 emit-rep-stos "rep ; stos")
(define-insn-0 emit-pushf "pushf")
(define-insn-0 emit-popf "popf")

(define (emit-scale-number cg scale oper)
  (cond ((number? oper)
         (ash oper (- scale number-tag-bits)))
        ((register? oper)
         (cond ((< scale number-tag-bits)
                (begin (emit-sar cg (- number-tag-bits scale) oper) oper))
               ((> scale number-tag-bits)
                (begin (emit-shl cg (- scale number-tag-bits) oper) oper))
               (true
                oper)))
        (true
         (error "emit-scale-number on operand ~A" oper))))

(define (emit-branch cg cc label)
  (emit-without-flushing "j~A ~A" cc label))

(define (emit-jump cg label)
  (emit-without-flushing "jmp ~A" label))

(define (emit-mov-maybe cg src dst)
  (unless (eq? src dst) (emit-mov cg src dst)))

(define (emit-branch-on-operand cg oper out-frame-base tlabel flabel)
  (emit-cmp cg false-representation oper)
  (emit-smart-branch cg "ne" out-frame-base tlabel flabel))

(define (emit-prepare-convert-cc-value cg reg)
  (emit-clear cg reg))

(define (emit-convert-cc-value cg cc reg)
  ;; this embeds special-tag and the representations of false and true
  (emit-set cg cc reg)
  (emit-shl cg special-tag-bits reg 0)
  (emit-or cg special-tag reg 0))

;;; Heap allocation

(define (emit-alloc cg tag-bits size allocreg spare-regs . scale)
  (let* ((again-label (gen-label))
         (exhausted-label (gen-label)))
    (flush-labels-and-jumps cg)
    (emit-raw-label cg again-label)
    (emit-mov cg (mem-operand "heap_alloc") allocreg)
    (emit-sub cg size allocreg)
    (set! scale (if (null? scale) value-scale (car scale)))
    (unless (= tag-bits scale) (emit-and cg (- (ash 1 tag-bits)) allocreg))
    (emit-cmp cg (mem-operand "heap_threshold") allocreg)
    (emit-branch cg "be" exhausted-label)
    (emit-mov cg allocreg (mem-operand "heap_alloc"))

    (codegen-push-out-of-line cg
      (lambda ()
        (emit-raw-label cg exhausted-label)
        (emit-mov cg (register-bitset (cons allocreg spare-regs)) %closure)
        (emit cg "call heap_exhausted")
        (emit-restore-%closure cg)
        (emit-jump cg again-label)))))

(define gc-label (make-label-for 'gc direct-function-label-prefix))

(define (codegen-helpers cg)
  (labels ((for-live-registers (regs op)
             (dolist (reg regs)
               (let* ((l (gen-label)))
                 (emit-test cg (register-bit reg) %closure)
                 (emit-branch cg "nz" l)
                 (funcall op cg reg)
                 (emit-raw-label cg l)))))
    (emit cg ".text")
    (emit cg ".globl heap_exhausted")
    (emit cg "heap_exhausted:")

    ;; Push live registers onto the stack, guided by the bitset in
    ;; %closure.  This preserves their values, and also means that
    ;; they get treated as part of the root set by the GC.
    (for-live-registers general-registers (function emit-push))

    ;; We need to save the live reg bitset from %closure, in order to
    ;; restore the live registers after the GC.  But if we put it on
    ;; the stack it it's original form, the GC will see it and try to
    ;; interpret it as an object reference.  So we disguise it as a
    ;; fixnum first.
    (emit-shl cg number-tag-bits %closure)
    (emit-push cg %closure)
    (emit cg "call ~A" gc-label)
    (emit-pop cg %closure)
    (emit-sar cg number-tag-bits %closure)

    ;; Restore live registers
    (for-live-registers (reverse general-registers) (function emit-pop))

    (emit cg "ret")))

;; the gc uses the raw-alloc operation

(define-operator (raw-alloc (size reg-imm)) ((temp-regs alloc))
  (emit-alloc cg (compiler-constant-value (attr-ref attrs 'tag-bits))
              (emit-scale-number cg value-scale size) alloc spare-regs)
  (emit-mov cg alloc result)
  result)

;;; Functions and closures

;; alloc-closure simply allocates space for the closure, it does not
;; initialize its contents.  This is because it will always be
;; enclosed by a lambda that does the initialization.
(define-operator (alloc-closure) ()
  (emit-alloc cg closure-tag-bits (* value-size (1+ (attr-ref attrs 'length)))
              result spare-regs)
  (emit-mov cg (attr-ref attrs 'label) (mem-operand result))
  (emit-add cg closure-tag result)
  result)

(define (closure-slot closure index)
  (tagged-operand closure-tag closure (1+ index)))

(define (emit-store-into-closure cg closure index val)
  (emit-mov cg val (closure-slot closure index)))

(define-operator (set-closure! closure (val reg-imm)) ()
  (emit-store-into-closure cg closure (attr-ref attrs 'index) val)
  val)

(define (emit-call-or-jump cg insn func nargs)
  (let* ((label (prepare-for-call cg func nargs))
         (comment (comment-form func)))
    (if label
        (emit cg "~A ~A # ~S" insn label comment)
        (emit cg "~A *~A # ~S" insn
              (value-sized (tagged-operand closure-tag %closure 0))
              comment))))

(define-codegen (call attrs func . args)
  (with-saved-frame-base cg
    (codegen-call-args cg func args)
    (emit-call-or-jump cg "call" func (length args)))
  (emit-restore-%closure cg)
  %funcres)

(define (emit-restore-%closure cg)
  (when (codegen-have-closure cg)
    (emit-mov cg (closure-address-slot cg) %closure)))

;;; Variables

(define (varrec-operand varrec cg)
  (let* ((mode (varrec-mode varrec)))
    (cond ((eq? mode 'top-level) (mem-operand (varrec-index varrec)))
          ((eq? mode 'param) (param-slot cg (varrec-index varrec)))
          ((eq? mode 'local) (local-slot cg (varrec-index varrec)))
          ((eq? mode 'closure) (closure-slot %closure (varrec-index varrec)))
          ((eq? mode 'self) %closure)
          (true (error "strange variable mode ~S" mode)))))

(define-operator (define (val reg-mem-imm)) ()
  (emit-frame-push cg val)
  (varrec-set-index! (attr-ref attrs 'varrec) (codegen-frame-base cg))
  val)

(define-operator (set! (val reg-imm)) ()
  (emit-mov cg val (varrec-operand (attr-ref attrs 'varrec) cg))
  val)

(define-operator (ref) () (varrec-operand (attr-ref attrs 'varrec) cg))

;;; Function call-related internals

(define (emit-check-nargs cg nparams bad-nargs-label)
  (emit-cmp cg (fixnum-representation nparams) %nargs)
  (emit-branch cg "ne" bad-nargs-label))

(define-operator (raw-args-base) ()
  (emit-lea cg (param-slot cg 0) result)
  result)

;;; Comparisons

(defmacro (define-cmp-operator name cc)
  (quasiquote (define-cc-operator ((unquote name) a (b reg-mem-imm)) ()
                (emit-cmp cg b a)
                (unquote cc))))

(define-cmp-operator eq? "e")
(define-cmp-operator = "e")
(define-cmp-operator /= "ne")
(define-cmp-operator > "g")
(define-cmp-operator >= "ge")
(define-cmp-operator < "l")
(define-cmp-operator <= "le")

;;; Conses

(define-operator (cons (a reg-imm) (d reg-imm)) ((temp-regs alloc))
  (emit-alloc cg pair-tag-bits (* 2 value-size) alloc spare-regs)
  (emit-mov cg a (mem-operand alloc))
  (emit-mov cg d (indexed-operand alloc 1))
  (emit-lea cg (offset-operand alloc pair-tag) result)
  result)

(define-operator (car a) () (tagged-operand pair-tag a 0))
(define-operator (cdr a) () (tagged-operand pair-tag a 1))

(define-operator (rplaca c (a reg-imm)) ()
  (emit-mov cg a (tagged-operand pair-tag c 0))
  c)

(define-operator (rplacd c (d reg-imm)) ()
  (emit-mov cg d (tagged-operand pair-tag c 1))
  c)

;;; Boxes

(define-operator (raw-make-box (val reg-imm)) ((temp-regs alloc))
  (emit-alloc cg box-tag-bits value-size alloc spare-regs)
  (emit-mov cg val (mem-operand alloc))
  (emit-lea cg (offset-operand alloc box-tag) result)
  result)

(define-operator (raw-box-set! box (val reg-imm)) ()
  (emit-mov cg val (tagged-operand box-tag box 0))
  val)

(define-operator (raw-box-ref box) ()
  (tagged-operand box-tag box 0))

;;; Symbols

(define-operator (symbol-name sym) ()
  (tagged-operand symbol-tag sym 0))

(define-operator (symbol-id sym) ()
  (tagged-operand symbol-tag sym 1))

(define-operator (raw-make-symbol (str reg-imm) (id reg-imm))
    ((temp-regs alloc))
  (emit-alloc cg symbol-tag-bits (* 2 value-size) alloc spare-regs)
  (emit-mov cg str (mem-operand alloc))
  (emit-mov cg id (indexed-operand alloc 1))
  (emit-lea cg (offset-operand alloc symbol-tag) result)
  result)

;;;  Numbers

(define-operator (+ a (b reg-mem-imm)) ()
  (emit-add cg b a)
  a)

(define-operator (* a (b reg-mem-imm)) ()
  (if (number? b)
      (set! b (ash b (- number-tag-bits)))
      (emit-sar cg number-tag-bits a))
  (emit-imul cg b a)
  a)

(define-operator (negate a) ()
  (emit-neg cg a)
  a)

(define-operator (- a (b reg-mem-imm)) ()
  (emit-sub cg b a)
  a)

(define-operator (truncate (dividend fixed-reg %a) (divisor reg-mem))
                 ((temp-fixed-regs %d))
  (emit-mov cg dividend %d)
  (emit-extend-sign-bit cg %d)
  (emit-idiv cg divisor)
  (emit-shl cg number-tag-bits dividend)
  dividend)

(define-operator (rem (dividend fixed-reg %a) (divisor reg-mem))
                 ((temp-fixed-regs %d))
  (emit-mov cg dividend %d)
  (emit-extend-sign-bit cg %d)
  (emit-idiv cg divisor)
  %d)

(define-operator (ash-imm n) ()
  (let* ((shift (attr-ref attrs 'shift)))
    (if (>= shift 0)
        (emit-shl cg shift n)
        (begin
          (emit-sar cg (- shift) n)
          (emit-and cg (ash -1 number-tag-bits) n))))
  n)

(define-operator (ash n (shift fixed-reg %c)) ()
  (let* ((l1 (gen-label))
         (l2 (gen-label))
         (l3 (gen-label))
         (l4 (gen-label))
         (l5 (gen-label)))
    (emit-sar cg number-tag-bits %c)
    (emit-branch cg "s" l1)
    ;; n >= 0
    (emit-cmp cg value-bits %c)
    (emit-branch cg "ae" l2)
    ;; 0 <= n < value-bits
    (emit cg "shl~C %cl, ~A" (string-ref insn-size-suffixes value-scale)
          (value-sized n))
    (emit-jump cg l5)

    (emit-raw-label cg l2)
    ;; n >= value-bits
    (emit-clear cg n)
    (emit-jump cg l5)

    (emit-raw-label cg l1)
    ;; n < 0
    (emit-cmp cg (- value-bits) %c)
    (emit-branch cg "be" l3)
    (emit-neg cg %c)
    (emit cg "sar~C %cl, ~A" (string-ref insn-size-suffixes value-scale)
          (value-sized n))
    (emit-jump cg l4)

    (emit-raw-label cg l3)
    ;; n <= -value-bits
    (emit-sar cg (- value-bits 1) n)

    (emit-raw-label cg l4)
    (emit-and cg (ash -1 number-tag-bits) n)
    (emit-raw-label cg l5))
  n)

(define-operator (logand a (b reg-mem-imm)) ()
  (emit-and cg b a)
  a)

(define-operator (logior a (b reg-mem-imm)) ()
  (emit-or cg b a)
  a)

(define-operator (logxor a (b reg-mem-imm)) ()
  (emit-xor cg b a)
  a)

;;; Strings and vectors

(define-operator (make-vec len (init fixed-reg %a))
                 ((temp-regs alloc) (temp-fixed-regs %c %di))
  (let* ((tag (attr-ref attrs 'tag))
         (tag-bits (attr-ref attrs 'tag-bits))
         (scale (attr-ref attrs 'scale)))
    (emit-mov cg len %c)
    (emit-scale-number cg scale len)
    (emit-add cg value-size len)
    (emit-alloc cg tag-bits len alloc (list* %di spare-regs) scale)
    (emit-mov cg alloc %di)
    (emit-mov cg %c (mem-operand alloc))
    (emit-add cg value-size %di)
    (emit-scale-number cg 0 %c)
    (emit-rep-stos cg scale)
    (emit-lea cg (offset-operand alloc tag) result))
  result)

(define-operator (vec-length vec) ()
  (tagged-operand (attr-ref attrs 'tag) vec 0))

(define (vec-slot cg attrs vec index)
  (let* ((offset (- (attr-ref attrs 'header-size) (attr-ref attrs 'tag)))
         (scaled-index  (emit-scale-number cg (attr-ref attrs 'scale) index)))
    (if (number? scaled-index)
        (offset-operand vec (+ offset scaled-index))
        (offset-2-operand vec scaled-index offset))))

(define-operator (vec-address vec (index reg-imm)) ()
  (emit-lea cg (vec-slot cg attrs vec index) result)
  result)

(define-operator (raw-vec-ref vec (index reg-imm)) ()
  (let* ((slot (vec-slot cg attrs vec index))
         (scale (attr-ref attrs 'scale)))
    (if (and (= scale value-scale) (immediate? index))
        ;; The slot is a suitable memory operand to return directly.
        ;; The 'immediate? index' condition is important, due to its
        ;; interaction with the code generation strategy: It ensures
        ;; that we only return memory operands that involve the'vec'
        ;; register that was reserved for our result anyway.  If that
        ;; condition is omitted, we might produce an operand that
        ;; involves the 'index' register, and that might get trampled
        ;; on before the memory operand is used.
        slot
        (begin (emit-movzx cg slot result scale)
               result))))

(define-operator (raw-vec-set! vec (index reg-imm) (val reg-imm)) ()
  (emit-mov cg val (vec-slot cg attrs vec index) (attr-ref attrs 'scale))
  val)

(define-operator (copy-mem (src-addr fixed-reg %si) (dest-addr fixed-reg %di)
                           (len fixed-reg %c))
                 ((temp-regs scaled-len dest-delta))
  (let* ((tag (attr-ref attrs 'tag))
         (scale (attr-ref attrs 'scale))
         (l1 (gen-label))
         (l2 (gen-label)))
    (if (= scale 0)
        (begin
          (emit-sar cg number-tag-bits len)
          (emit-mov cg dest-addr dest-delta)
          (set! scaled-len len))
        (begin
          (emit-mov cg len scaled-len)
          (emit-sar cg number-tag-bits len)
          (emit-mov cg dest-addr dest-delta)
          (emit-scale-number cg scale scaled-len)))
    (emit-sub cg src-addr dest-delta)
    (emit-cmp cg scaled-len dest-delta)
    (emit-branch cg "b" l1)

    ;; Can copy forwards
    (emit-rep-movs cg scale)
    (emit-jump cg l2)

    (emit-raw-label cg l1)
    ;; Need to copy backwards
    (emit cg "std")
    (emit-lea cg (offset-2-operand src-addr scaled-len (ash -1 scale))
              src-addr)
    (emit-lea cg (offset-2-operand dest-addr scaled-len (ash -1 scale))
              dest-addr)
    (emit-rep-movs cg scale)
    (emit cg "cld")

    (emit-raw-label cg l2))
  unspecified-representation)

;;; Raw memory access

(define-operator (raw-ref (addr reg-imm)) ()
  (emit-movzx cg (mem-operand addr) result (attr-ref attrs 'scale))
  result)

(define-operator (raw-set! (addr reg-imm) (val reg-imm)) ()
  (emit-mov cg val (mem-operand addr) (attr-ref attrs 'scale))
  val)

;;; Raw ops for the GC

(define-operator (raw-logand a (b reg-mem-imm)) ()
  (emit-and cg b a)
  a)

(define-operator (raw-- a (b reg-mem-imm)) ()
  (emit-sub cg b a)
  a)

(define-operator (raw-+ a (b reg-mem-imm)) ()
  (emit-add cg b a)
  a)

;;; Misc runtime support

(define-operator (fixnum->raw (val reg-imm)) ()
  (if (immediate? val)
      (set! val (ash val (- number-tag-bits)))
      (emit-sar cg number-tag-bits val))
  val)

(define-operator (raw->fixnum (val reg-imm)) ()
  (if (immediate? val)
      (set! val (fixnum-representation val))
      (emit-shl cg number-tag-bits val))
  val)

(define-operator (raw-label) () (attr-ref attrs 'name))

(define-operator (raw-global-set! (val reg-imm)) ()
  (emit-mov cg val (mem-operand (subject-language-symbol-name
                                 (attr-ref attrs 'name))))
  val)

(define-operator (raw-global-ref) ()
  (mem-operand (subject-language-symbol-name (attr-ref attrs 'name))))

(define (emit-set-ac-flag cg enable)
  (emit-pushf cg)
  (if enable
      (emit-or cg #x40000 (mem-operand %sp) 2)
        ;; we can't use an immediate mask value, due to fixnum limitations
      (let* ((reg (first general-registers)))
        (emit-mov cg #x40000 reg 2)
        (emit-not cg reg 2)
        (emit-and cg reg (mem-operand %sp) 2)))
  (emit-popf cg))

