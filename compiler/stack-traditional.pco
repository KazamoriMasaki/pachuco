;;; Common parts of "traditional" x86 stack regimes.  That is, with
;;; %sp marking the bottom of the stack, and the parameters above the
;;; return address.

(define (copy-tail-call-args cg out-arg-count out-retaddr tmpreg)
  (when (> out-arg-count 0)
    (set! out-arg-count (1- out-arg-count))
    (emit-mov cg (indexed-operand %sp out-arg-count) tmpreg)
    (emit-mov cg tmpreg (reindex-operand out-retaddr (1+ out-arg-count)))
    (copy-tail-call-args cg out-arg-count out-retaddr tmpreg)))

(define (emit-frame-push cg reg)
  (emit-push cg reg)
  (codegen-set-frame-base! cg (1+ (codegen-frame-base cg))))

(define (emit-frame-pop cg reg)
  (emit-pop cg reg)
  (codegen-set-frame-base! cg (1- (codegen-frame-base cg))))

(define (emit-frame-spill cg operand reg)
  ;; Push can take any operand
  (emit-frame-push cg operand))

(define (emit-reset-frame-base cg out-frame-base)
  (when out-frame-base
    (let* ((in-frame-base (codegen-frame-base cg)))
      (unless (= in-frame-base out-frame-base)
        ;; Use lea here to avoid trashing condition codes
        (emit-lea cg (indexed-operand %sp (- in-frame-base out-frame-base)) %sp)
        (codegen-set-frame-base! cg out-frame-base)))))
