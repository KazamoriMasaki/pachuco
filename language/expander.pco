;;; Macro expansion

(define builtin-expanders (make-symboltable))

;; The macro environment consists of a pair of lalists, the first for
;; normal macro expanders, the second for symbol macro expansions.

(define (make-initial-macro-env)
  (list (cons false false) (cons builtin-expanders false)))

(defmacro (define-form-expander keyword . body)
  (quasiquote
    (symboltable-set! builtin-expanders (unquote keyword)
                      (lambda (form keyword args dual-env)
                        (unquote-splicing body)))))

(define (add-form-expander macro-env keyword expander)
  (unless (caar macro-env) (rplaca (car macro-env) (make-symboltable)))
  (symboltable-set! (caar macro-env) keyword expander))

(define (macro-env-expander macro-env keyword)
  (and (not (null? macro-env))
       (let* ((res (and (caar macro-env)
                        (symboltable-ref (caar macro-env) keyword))))
         (or res (macro-env-expander (cdr macro-env) keyword)))))

(define (add-symbol-expansion macro-env sym expansion)
  (unless (cdar macro-env) (rplacd (car macro-env) (make-symboltable)))
  (symboltable-set! (cdar macro-env) sym (cons expansion ())))

(define (macro-env-expansion-cell macro-env sym)
  (and (not (null? macro-env))
       (let* ((res (and (cdar macro-env)
                        (symboltable-ref (cdar macro-env) sym))))
         (or res (macro-env-expansion-cell (cdr macro-env) sym)))))

;; A dual environment consits of a pair of the macro environment and
;; the evaluation environment for evaluation of macros.

(define (expand-form form dual-env)
  (cond ((pair? form)
         (let* ((keyword (car form))
                (expander (and (symbol? keyword)
                               (macro-env-expander (car dual-env) keyword))))
           (if expander
               (funcall expander form keyword (cdr form) dual-env)
               (mapfor (subform form) (expand-form subform dual-env)))))
        ((symbol? form)
         (let* ((cell (macro-env-expansion-cell (car dual-env) form)))
           (if cell
               (expand-form (car cell) dual-env)
               form)))
        (true
         form)))

(define (expand-define keyword args dual-env)
  ;; normalize and expand a let/set!/define/defmacro
  (let* ((lhs (car args)))
    (cond ((pair? lhs)
           (list (car lhs)
                 (list* 'lambda (cdr lhs) (expand-body (cdr args) dual-env))))
          ((symbol? lhs)
           (if (null? (cdr args)) args
               (list lhs (expand-form (second args) dual-env))))
          (true
           (error "strange arguments to ~S: ~S" keyword args)))))

(define (do-defmacro args dual-env)
  (if (pair? (first args))
      (let* ((def (expand-define 'defmacro args dual-env))
             (macro-lambda (eval-form (second def) (cdr dual-env))))
        (add-form-expander (car dual-env) (first def)
                           (lambda (form keyword args dual-env)
                             (expand-form (funcall macro-lambda args)
                                          dual-env))))
      (add-symbol-expansion (car dual-env) (first args) (second args)))
  '(definitions))

(define (expand-body-form form dual-env)
  (set! form (expand-form form dual-env))
  (if (and (pair? form) (eq? 'defmacro (first form)))
      (do-defmacro (cdr form) dual-env)
      form))

(define (expand-body body dual-env)
  (mapfor (form body) (expand-body-form form dual-env)))

(define-form-expander 'quote
  form)

(define-form-expander 'lambda
  (list* keyword (car args) (expand-body (cdr args) dual-env)))

(define-form-expander 'begin
  (cons keyword (expand-body args (cons (cons (cons false false) (car dual-env))
                                        (cdr dual-env)))))

(define-form-expander 'definitions
  (cons keyword (expand-body args dual-env)))

(dolist (k '(define let set!))
  (define-form-expander k
      (cons keyword (expand-define keyword args dual-env))))

(define-form-expander 'defmacro
    ;; defmacros are left unexpanded until do-defmacro deals with them
    form)
