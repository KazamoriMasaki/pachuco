;;; Runtime and library of basic definitions

;;; Basics needed by quasiquote

(define (list . l) l)
(defmacro (list . l)
  (define (liszt . l) l)
  (define (aux l)
    (if (eq? () l) ()
        (liszt 'cons (car l) (aux (cdr l)))))
  (aux l))

(defmacro (caar x) (list 'car (list 'car x)))
(defmacro (cadr x) (list 'car (list 'cdr x)))
(defmacro (cdar x) (list 'cdr (list 'car x)))
(defmacro (cddr x) (list 'cdr (list 'cdr x)))
(defmacro (cadar x) (list 'car (list ' cdr (list 'car x))))
(defmacro (cdddr x) (list 'cdr (list ' cdr (list 'cdr x))))
(defmacro (cddddr x) (list 'cdr (list 'cdr (list 'cdr (list 'cdr x)))))

(defmacro (first x) (list 'car x))
(defmacro (second x) (list 'car (list 'cdr x)))
(defmacro (third x) (list 'car (list 'cdr (list 'cdr x))))
(defmacro (fourth x) (list 'car (list 'cdr (list 'cdr (list 'cdr x)))))
(defmacro (fifth x) (list 'car (list 'cdr (list 'cdr (list 'cdr (list 'cdr x))))))

(defmacro (null? x) (list 'eq? () x))

(define (reduce initial l op)
  (if (null? l) initial
      (reduce (op initial (car l)) (cdr l) op)))

(defmacro (and . args)
  (if (null? args) #t
      (if (null? (cdr args)) (car args)
          (reduce (car args) (cdr args) (lambda (a b) (list 'if a b #f))))))

(defmacro (cond . clauses)
  (if (null? clauses) '(begin)
      (list 'if (caar clauses) (cons 'begin (cdar clauses))
            (cons 'cond (cdr clauses)))))

;;; Quasiquote and prerequisites

(define (quasiquote-transform innermost form)
  (cond ((pair? form)
         (define keyword (car form))
         (cond ((eq? 'unquote keyword)
                (if innermost (cadr form)
                    (list 'list '(quote quote) (cadr form))))
               ((eq? 'quasiquote keyword)
                (quasiquote-transform #f
                                  (quasiquote-transform innermost (cadr form))))
               ((and (pair? keyword) (eq? 'unquote-splicing (car keyword)))
                (list 'append (cadr keyword)
                      (quasiquote-transform innermost (cdr form))))
               (#t
                (list 'cons (quasiquote-transform innermost keyword)
                            (quasiquote-transform innermost (cdr form))))))
        ((symbol? form)
         (list 'quote form))
        (#t form)))

(defmacro (quasiquote form)
  (quasiquote-transform #t form))

(define (append-2 c l1 rest)
  ;; Attach elements from the list l1 and list of lists rest onto the cons c
  (if (null? l1)
      (begin
        (define l2 (car rest))
        (define rest2 (cdr rest))
        (if (null? rest2)
          (rplacd c l2) ; only one more list, so stick it on the end
          (append-2 c l2 rest2)))
      (begin
        (define c2 (cons (car l1) ()))
        (rplacd c c2)
        (append-2 c2 (cdr l1) rest))))

(define (append-1 lists)
  ;; Finds the first non-empty list, and hands over to append-2
  (define l1 (car lists))
  (define rest (cdr lists))
  (if (null? rest) l1 ; only one list, so that is the answer
      (if (null? l1) (append-1 rest) ; list empty, move on to the next
          (begin
            (define c (cons (car l1) ()))
            (append-2 c (cdr l1) rest)
            c))))

(define (append . lists)
  (if (null? lists) ()
      (append-1 lists)))

;;; Reified builtins

(defmacro (reify form)
  (quasiquote (define (unquote form) (unquote form))))

(reify (eq? a b))
(reify (function? a))

(reify (symbol? a))
(reify (symbol-name a))
(reify (symbol-id a))

(reify (pair? a))
(reify (null? a))
(reify (car a))
(reify (cdr a))
(reify (cons a b))
(reify (rplaca a b))
(reify (rplacd a b))

(reify (number? a))
(reify (< a b))
(reify (<= a b))
(reify (> a b))
(reify (>= a b))
(reify (= a b))
(reify (/= a b))

(define (+ . args)
  (if (null? args) 0
      (if (null? (cdr args)) (car args)
          (reduce (car args) (cdr args) (lambda (a b) (+ a b))))))

(define (* . args)
  (if (null? args) 1
      (if (null? (cdr args)) (car args)
          (reduce (car args) (cdr args) (lambda (a b) (* a b))))))

(define (- a . args)
  (if (null? args) (- a)
      (reduce a args (lambda (a b) (- a b)))))

(reify (rem a b))
(reify (truncate a b))
(reify (ash a b))
(reify (logand a b))
(reify (logior a b))
(reify (logxor a b))

(reify (character? a))
(reify (character-code a))
(reify (code-character a))

(reify (string? a))
(reify (make-string a init))
(reify (string-length a))

(reify (vector? a))
(reify (make-vector a init))
(reify (vector-length a))

;;; Numbers

(defmacro (1- n) (quasiquote (- (unquote n) 1)))
(defmacro (1+ n) (quasiquote (+ (unquote n) 1)))

;;; Booleans

(defmacro (not a) (quasiquote (if (unquote a) #f #t)))

(defmacro (or . args)
  (if (null? args) #f
      (if (null? (cdr args)) (car args)
          (reduce (car args) (cdr args)
                  (lambda (a b)
                    (define tmp (gensym))
                    (quasiquote (begin (define (unquote tmp) (unquote a))
                                       (if (unquote tmp) (unquote tmp)
                                           (unquote b)))))))))

(defmacro (when test . rest)
  (quasiquote (if (unquote test) (begin . (unquote rest)))))

(defmacro (unless test . rest)
  (quasiquote (if (not (unquote test)) (begin . (unquote rest)))))

;;; Control structures

(defmacro (while test . rest)
  (define name (gensym))
  (quasiquote (begin
               (define ((unquote name))
                   (when (unquote test)
                     (unquote-splicing rest)
                     ((unquote name))))
               ((unquote name)))))

(defmacro (until test . rest)
  (quasiquote (while (not (unquote test)) . (unquote rest))))

(define (identity x) x)

;;; The real definition of error requires formatted output, which is a
;;; bit late in the day.  So put a stub definition in place here.
(define (error message . args)
  (error-halt))

;;; Lists

(define (length-aux l n)
  (if (null? l) n (length-aux (cdr l) (1+ n))))

(define (length l)
  (length-aux l 0))

(define (member? item l)
  (cond ((null? l) #f)
        ((eq? item (car l)) #t)
        (#t (member? item (cdr l)))))

(define (adjoin item l)
  (if (member? item l) l (cons item l)))

(define (elt l index)
  (if (= 0 index) (car l) (elt (cdr l) (1- index))))

(define (nthcdr index l)
  (if (= 0 index) l (nthcdr (1- index) (cdr l))))

(define (last-n-conses l n)
  (define (aux l)
    (if (null? l) ()
        (begin
          (define res (aux (cdr l)))
          (if (= n 0) res
              (begin
                (set! n (1- n))
                l)))))
  (aux l))

(define (last-cons l)
  (last-n-conses l 1))

(define (last-elem l)
  (car (last-cons l)))

(define (list* l1 . l)
  (if (null? l) l1
    (begin
      (set! l (cons l1 l))
      (define penultimate (last-n-conses l 2))
      (rplacd penultimate (cadr penultimate))
      l)))

(defmacro (list* l1 . l)
  (define (aux l1 l)
    (if (null? l) l1
        (quasiquote (cons (unquote l1) (unquote (aux (car l) (cdr l)))))))
  (aux l1 l))

(define (copy-list l)
  (if (pair? l) (cons (car l) (copy-list (cdr l))) l))

(define (copy-tree l)
  (if (pair? l)
      (cons (copy-tree (car l)) (copy-tree (cdr l)))
      l))

(define (flatten* ls)
  (define (find-end l ls)
    (if (null? (cdr l))
        (find-start l ls)
        (find-end (cdr l) ls)))

  (define (find-start l ls)
    (unless (null? ls)
      (if (null? (car ls))
          (find-start l (cdr ls))
          (begin
            (rplacd l (car ls))
            (find-end (car ls) (cdr ls))))))

  (if (null? ls) ()
      (if (null? (car ls))
          (flatten* (cdr ls))
          (begin
            (find-end (car ls) (cdr ls))
            (car ls)))))

(define (nconc . ls)
  (flatten* ls))

(defmacro (dolist binding . body)
  (define l (gensym))
  (quasiquote
    (begin
      (define (unquote l) (unquote (second binding)))
      (until (null? (unquote l))
        (begin
          (define (unquote (first binding)) (car (unquote l)))
          . (unquote body))
        (set! (unquote l) (cdr (unquote l)))))))

(defmacro (mapfor binding . body)
  (define bodyf (gensym))
  (define loopf (gensym))
  (define in (gensym))
  (define out (gensym))
  (define tail (gensym))
  (quasiquote (begin
    (define (unquote in) (unquote (second binding)))
    (if (null? (unquote in)) ()
        (begin
          (define ((unquote bodyf) (unquote (first binding)))
              . (unquote body))

          (define (unquote out)
              (cons ((unquote bodyf) (car (unquote in))) ()))
          (define (unquote tail) (unquote out))
          (define ((unquote loopf) (unquote in))
            (unless (null? (unquote in))
              (define (unquote out)
                  (cons ((unquote bodyf) (car (unquote in))) ()))
              (rplacd (unquote tail) (unquote out))
              (set! (unquote tail) (unquote out))
              ((unquote loopf) (cdr (unquote in)))))

          ((unquote loopf) (cdr (unquote in)))
          (unquote out))))))

(defmacro (nmapfor binding . body)
  (define loopf (gensym))
  (define l (gensym))
  (quasiquote (begin
    (define ((unquote loopf) (unquote l))
      (unless (null? (unquote l))
        (define (unquote (first binding)) (car (unquote l)))
        (rplaca (unquote l) (begin . (unquote body)))
        ((unquote loopf) (cdr (unquote l)))))
    (define (unquote l) (unquote (second binding)))
    ((unquote loopf) (unquote l))
    (unquote l))))

(defmacro (flatten*-mapfor binding . body)
  (quasiquote (flatten* (mapfor (unquote binding) . (unquote body)))))

(defmacro (findfor binding . body)
  (define loopf (gensym))
  (define l (gensym))
  (quasiquote (begin
    (define ((unquote loopf) (unquote l))
      (if (null? (unquote l)) #f
          (begin (define (unquote (first binding)) (car (unquote l)))
                 (if (begin . (unquote body)) (unquote (first binding))
                     ((unquote loopf) (cdr (unquote l)))))))
    ((unquote loopf) (unquote (second binding))))))

(defmacro (filterfor binding . body)
  (quasiquote
    (flatten*-mapfor (unquote binding)
      (if (begin . (unquote body))
          (list (unquote (first binding)))
          ()))))

(defmacro (nfilterfor binding . body)
  (define loopf (gensym))
  (define in (gensym))
  (define out (gensym))
  (define out-tail (gensym))
  (quasiquote (begin
     (define (unquote out) (cons () ()))
     (define ((unquote loopf) (unquote in) (unquote out-tail))
       (if (not (null? (unquote in)))
         ((unquote loopf) (cdr (unquote in))
           (if (begin (define (unquote (first binding)) (car (unquote in)))
                      . (unquote body))
               (begin (rplacd (unquote out-tail) (unquote in))
                      (unquote in))
               (unquote out-tail)))
         (rplacd (unquote out-tail) ())))
     ((unquote loopf) (unquote (second binding)) (unquote out))
     (cdr (unquote out)))))

(define (remove item l)
  (cond ((null? l) l)
        ((eq? item (car l)) (remove item (cdr l)))
        (#t (cons (car l) (remove item (cdr l))))))

(define (delete item l)
  (define (aux c l)
    (unless (null? l)
      (if (eq? item (car l))
          (begin
            (rplacd c (cdr l))
            (aux c (cdr l)))
          (aux l (cdr l)))))

  (if (null? l) ()
      (if (eq? item (car l))
          (delete item (cdr l))
          (begin
            (aux l (cdr l))
            l))))

(define (sort-aux reverse l pred)
  (cond ((null? l) l)
        ((null? (cdr l)) l)
        ((null? (cddr l))
         (define l1 (first l))
         (define l2 (second l))
         (if (if (pred l1 l2) (not reverse) reverse) l (list l2 l1)))

        (#t
         ;; partition the list into two halves
         (define l1 ())
         (define l2 ())

         (define (split l)
           (unless (null? l)
             (set! l1 (cons (car l) l1))
             (set! l (cdr l))

             (unless (null? l)
               (set! l2 (cons (car l) l2))
               (split (cdr l)))))

         (split l)

         ;; sort and merge the halves
         (define (merge l1 l2 res)
           (define t)
           (when (if (pred (car l1) (car l2)) (not reverse) reverse)
             (set! t l1)
             (set! l1 l2)
             (set! l2 t))

           (set! t (cdr l1))
           (if (null? t)
               (nreverse l2 l1 res)
               (merge t l2 (rplacd l1 res))))

         (define (nreverse l c res)
           (rplacd c res)
           (if (null? l)
               c
               (nreverse (cdr l) l c)))

         ;; merge reverses l1 and l2, so we need to pre-sort them in
         ;; the opposite sense.
         (define not-reverse (not reverse))
         (merge (sort-aux not-reverse l1 pred) (sort-aux not-reverse l2 pred)
                ()))))

(define (sort l pred)
  (sort-aux #f l pred))

(define (reverse l)
  (define (aux acc l)
    (if (null? l) acc
        (aux (cons (car l) acc) (cdr l))))
  (aux () l))

(define (sublist l start . end)
  (if (null? end) (copy-list (nthcdr start l))
      (begin
        (define (copy-partial-list l len)
          (cond ((= 0 len) ())
                ((not (null? l))
                 (cons (car l) (copy-partial-list (cdr l) (1- len))))
                (#t
                 (error "sublist fell off the end of the list"))))
        (copy-partial-list (nthcdr start l) (- (first end) start)))))

(define (position x l)
  (define (aux n l)
    (cond ((null? l) #f)
          ((eq? x (car l)) n)
          (#t (aux (1+ n) (cdr l)))))
  (aux 0 l))

;;; Numbers again

(define (mod n div)
  (define r (rem n div))
  (if (>= r 0) r (+ r div)))

(define (abs n)
  (if (>= n 0) n (- n)))

(defmacro (max init . nums)
  (reduce init nums (lambda (a b)
                      (define avar (gensym))
                      (define bvar (gensym))
                      (quasiquote (begin
                                    (define (unquote avar) (unquote a))
                                    (define (unquote bvar) (unquote b))
                                    (if (> (unquote avar) (unquote bvar))
                                        (unquote avar)
                                        (unquote bvar)))))))

(define (max$ init nums)
  (reduce init nums (lambda (a b) (max a b))))

(define (max init . nums)
  (max$ init nums))


(defmacro (min init . nums)
  (reduce init nums (lambda (a b)
                      (define avar (gensym))
                      (define bvar (gensym))
                      (quasiquote (begin
                                    (define (unquote avar) (unquote a))
                                    (define (unquote bvar) (unquote b))
                                    (if (< (unquote avar) (unquote bvar))
                                        (unquote avar)
                                        (unquote bvar)))))))

(define (min$ init nums)
  (reduce init nums (lambda (a b) (min a b))))

(define (min init . nums)
  (min$ init nums))


;;; Strings

(define (check-string-index str index)
  (define str-len (string-length str))
  (unless (and (>= index 0) (< index str-len))
    (error "string index out of bounds (index: ~A, string length: ~A)"
           index str-len)))

(define (check-string-range str offset len)
  (define str-len (string-length str))
  (unless (and (>= offset 0) (<= (+ offset len) str-len))
    (error "string range out of bounds (offset: ~A, length: ~A, string length: ~A)"
           offset len str-len)))

(define (string-ref str index)
  (check-string-index str index)
  (raw-string-ref str index))

(define (string-set! str index ch)
  (check-string-index str index)
  (raw-string-set! str index ch)
  ch)

(define (string-copy src src-offset dest dest-offset len)
  (check-string-range src src-offset len)
  (check-string-range dest dest-offset len)
  (raw-string-copy src src-offset dest dest-offset len))

;;; Vectors

(define (check-vector-index vec index)
  (unless (and (>= index 0) (< index (vector-length vec)))
    (error "vector index out of bounds (index: ~A, vector length: ~A)"
           index (vector-length vec))))

(define (check-vector-range vec offset len)
  (unless (and (>= offset 0) (<= (+ offset len) (vector-length vec)))
    (error "vector range out of bounds (offset: ~A, length: ~A, vector length ~A)"
           offset len (vector-length vec))))

(define (vector-ref vec index)
  (check-vector-index vec index)
  (raw-vector-ref vec index))

(define (vector-set! vec index val)
  (check-vector-index vec index)
  (raw-vector-set! vec index val)
  val)

(define (vector-copy src src-offset dest dest-offset len)
  (check-vector-range src src-offset len)
  (check-vector-range dest dest-offset len)
  (raw-vector-copy src src-offset dest dest-offset len))

(define (vector-set-range! vec index len val)
  (while (/= len 0)
    (vector-set! vec index val)
    (set! index (1+ index))
    (set! len (1- len))))

(define (make-vector-from-list l)
  (define (copy-list-to-vector l vec pos)
    (unless (null? l)
      (vector-set! vec pos (car l))
      (copy-list-to-vector (cdr l) vec (1+ pos))))

  (define vec (make-vector (length l) #f))
  (copy-list-to-vector l vec 0)
  vec)

(define (vector . l)
  (make-vector-from-list l))

(defmacro (vector . l)
  (define v (gensym))

  (define (inits l pos)
    (if (null? l) ()
        (cons (list 'raw-vector-set! v pos (car l)) (inits (cdr l) (1+ pos)))))

  (quasiquote (begin
                (define (unquote v) (make-vector (unquote (length l)) #f))
                (unquote-splicing (inits l 0))
                (unquote v))))

(define (vector-equal? a b)
  (define len (vector-length a))
  (and (= (vector-length b) len)
       (begin
         (define (compare-elts pos)
           (or (= pos len)
               (and (equal? (vector-ref a pos) (vector-ref b pos))
                    (compare-elts (1+ pos)))))
         (compare-elts 0))))

;;; Lists

(define (assoc key l)
  (cond ((null? l) #f)
        ((eq? key (caar l)) (car l))
        (#t (assoc key (cdr l)))))

(defmacro (push el place)
  (quasiquote (set! (unquote place) (cons (unquote el) (unquote place)))))

(define (acons key val tail)
  (cons (cons key val) tail))

;;; Strings

(define (substring str offset len)
  (define newstr (make-string len (code-character 0)))
  (string-copy str offset newstr 0 len)
  newstr)

(define (string-equal? a b)
  (define len (string-length a))
  (and (= (string-length b) len)
       (begin
         (define (compare-chars pos)
           (or (= pos len)
               (and (eq? (string-ref a pos) (string-ref b pos))
                    (compare-chars (1+ pos)))))
         (compare-chars 0))))

(define (string< a b)
  (define min-len (min (string-length a) (string-length b)))

  (define (compare-chars pos)
    (if (= pos min-len)
        (< min-len (string-length b))
        (begin
          (define achar (character-code (raw-string-ref a pos)))
          (define bchar (character-code (raw-string-ref b pos)))
          (cond ((< achar bchar) #t)
                ((> achar bchar) #f)
                (#t (compare-chars (1+ pos)))))))

  (compare-chars 0))

(define (string-flatten strs)
  (define pos 0)
  (dolist (str strs)
    (set! pos (+ pos (string-length str))))
  (define res (make-string pos (code-character 0)))
  (set! pos 0)
  (dolist (str strs)
    (string-copy str 0 res pos (string-length str))
    (set! pos (+ pos (string-length str))))
  res)

(define (string-concat . strs)
  (string-flatten strs))

(define (string-range-equal? a astart b bstart len)
  (cond ((= 0 len) #t)
        ((or (>= astart (string-length a))
             (>= bstart (string-length b))
             (not (eq? (string-ref a astart) (string-ref b bstart))))
         #f)
        (#t
         (string-range-equal? a (1+ astart) b (1+ bstart) (1- len)))))

(define (string-search haystack needle start)
  (define len (string-length needle))
  (cond ((> (+ start len) (string-length haystack)) #f)
        ((string-range-equal? haystack start needle 0 len) start)
        (#t (string-search haystack needle (1+ start)))))

(define (string-replace str old new)
  (define (string-replace-from str start)
    (define pos (string-search str old start))
    (if pos
        (begin
          (define rest (+ pos (string-length old)))
          (define res (string-concat (substring str 0 pos) new
                           (substring str rest (- (string-length str) rest))))
          (string-replace-from res (+ pos (string-length new))))
        str))
  (string-replace-from str 0))

;;; Equality

(define (equal? a b)
  (cond ((pair? a)
         (and (pair? b) (equal? (car a) (car b)) (equal? (cdr a) (cdr b))))
        ((string? a)
         (and (string? b) (string-equal? a b)))
        ((vector? a)
         (and (vector? b) (vector-equal? a b)))
        (#t (eq? a b))))

;;; Program entry point
(define (runtime-main)
  (main)
  (flush-ostream stdout))

;; Not a full destructuring-bind
(defmacro (bind vars values . body)
  (quasiquote
    (apply (lambda (unquote vars) . (unquote body)) (unquote values))))

